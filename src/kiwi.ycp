{
  textdomain "cd-creator";

  import "CWM";
  import "CWMTab";
  import "CDCreator";
  import "FileUtils";
  import "Label";
  import "Kiwi";
  import "Popup";
  import "Summary";
  import "Wizard";

  include "cd-creator/dialogs.ycp";

  string initial_tab	= "config.xml";

  string images_dir	= "/usr/share/kiwi/image";

  // map of current Live ISO configuration
  map LiveISOConfig	= $[];

  // read available the images under /usr/share/kiwi/image/ directory
  list<term> GetAvailableImages (string subdir) {

    list<term> ret	= [];
    map out = (map) SCR::Execute (.target.bash_output, sformat ("ls %1/%2", images_dir, subdir));
    if (out["exit"]:0 != 0)
	return ret;
    foreach (string file, splitstring (out["stdout"]:"", "\n"), {
	if (file != "" && FileUtils::IsDirectory (sformat ("%1/%2/%3", images_dir, subdir, file)))
	{
	    ret	= add (ret, `item (`id(file), file, file == LiveISOConfig["isoboot"]:""));
	}
    });
    return ret;
  }

  /********************************************************************************************
   * widget handlers
   *******************************************************************************************/

  /**
   * Initialize the contents of richtext with selected software
   */
  define void InitSWRichText (string id) {
    // richtext header
    string rt	= Summary::AddHeader ("", _("Patterns"));
    foreach (string pattern, (list<string>) LiveISOConfig["addons"]:[], {
	rt	= Summary::AddListItem (rt, pattern);
    });
    // richtext header
    rt	= Summary::AddHeader (rt, _("Packages"));
    foreach (string package, (list<string>) LiveISOConfig["packages"]:[], {
	rt	= Summary::AddListItem (rt, package);
    });
    UI::ChangeWidget (`id (id), `Value, rt);
  }

  define symbol HandleSWSelection (string key, map event) {
    if (event["ID"]:nil == key)
    {
	    // FIXME packageSelector will show, but won't export selected packages???
	    if (packageSelector () != `cancel)
	    {
		LiveISOConfig["addons"]		= CDCreator::Config["addons"]:[];
		LiveISOConfig["packages"]	= CDCreator::Config["packages"]:[];
//FIXME save taboo-ed packages
		InitSWRichText ("rt_sw");
	    }
    }
    return nil;
  }

  define symbol HandleSWIgnore (string key, map event) {
    return nil;
  }

  // packages for 'boot'?
  define void InitSWBootRichText (string id) {
    // richtext header
    string rt	= Summary::AddHeader ("", _("Packages"));
    foreach (string package, ["filesystem", "glibc-locale", "devs", "kernel-default"], {
	rt	= Summary::AddListItem (rt, package);
    });
    UI::ChangeWidget (`id (id), `Value, rt);
  }

  // packages to be ignored? (taboo)
  define void InitSWIgnoreRichText (string id) {
    string rt	= "";
    foreach (string package, [], {
	rt	= Summary::AddListItem (rt, package);
    });
    UI::ChangeWidget (`id (id), `Value, rt);
  }

  /**
   * initialize the value of combo box with isoboot items
   */
  define void InitISOBootCombo (string id) {
    UI::ChangeWidget (`id (id), `Items, GetAvailableImages ("isoboot"));
  }

  // store the value of current isoboot
  define void StoreISOBootCombo (string key, map event) {
      LiveISOConfig["isoboot"]	= (string) UI::QueryWidget(`id(key), `Value);
  }
  // handler for combo box with isoboot items
  define symbol HandleISOBootCombo (string key, map event) {
    any id	= event["ID"]:nil;
    // store the value on exiting
    if (id == "prepare" || id == `next)
	StoreISOBootCombo (key, event);
    return nil;
  }

  /**
   * initialize the value of compress checkbox
   */
  define void InitCompressCheckBox (string id) {
    UI::ChangeWidget (`id (id), `Enabled, false);// FIXME squashfs not available on SP1?
    UI::ChangeWidget (`id (id), `Value, LiveISOConfig["compressed"]:false);
  }

  // store the value of compress checkbox
  define void StoreCompressCheckBox (string key, map event) {
      LiveISOConfig["compressed"]	= (boolean) UI::QueryWidget(`id(key), `Value);
  }
  // handler for compress checkbox
  define symbol HandleCompressCheckBox (string key, map event) {
    any id	= event["ID"]:nil;
    // store the value on exiting
    if (id == "prepare" || id == `next)
	StoreCompressCheckBox (key, event);
    return nil;
  }

  /**
   * initialize the value of version
   */
  define void InitVersion (string id) {
    UI::ChangeWidget (`id (id), `Value, sformat ("%1", LiveISOConfig["version"]:"1"));
  }

  // store the value of current version
  define void StoreVersion (string key, map event) {
      LiveISOConfig["version"]	= sformat ("%1", UI::QueryWidget(`id(key), `Value));
  }
  // handler for version
  define symbol HandleVersion (string key, map event) {
    any id	= event["ID"]:nil;
    // store the value on exiting
    if (id == "prepare" || id == `next)
	StoreVersion (key, event);
    return nil;
  }

  /**
   * initialize the value string value of widget @param
   */
  define void InitDescription (string id) {
    UI::ChangeWidget (`id (id), `Value, LiveISOConfig[id]:"");
  }

  // store the value of string widget
  define void StoreDescription (string key, map event) {
      LiveISOConfig[key]	= UI::QueryWidget (`id(key), `Value);
  }
  // handler for string widgets: store their value on exit/save
  define symbol HandleDescription (string key, map event) {
    any id	= event["ID"]:nil;
    // store the value on exiting
    if (id == "prepare" || id == `next)
	StoreDescription (key, event);
    return nil;
  }

  define void InitOutputDir (string id) {
    UI::ChangeWidget (`id (id), `Value, LiveISOConfig["iso-directory"]:"");
  }

  /*
  define void InitConfigSh (string id) {
    string file	= LiveISOConfig["config.sh"]:"";
    if (FileUtils::Exists (file))
    {
	string configsh	= (string) SCR::Read (.target.string, file);
	if (configsh != nil)
	    UI::ChangeWidget (`id (id), `Value, configsh);
    }
  }
  */

  /**
   * initialize the table with users
   */
  define void InitUsersTable (string id) {

    list items = maplist (string user, map usermap, (map<string,map>) LiveISOConfig["users"]:$[],
    ``(`item (`id (user), user, usermap["home"]:"")));
    UI::ChangeWidget (`id ("table"), `Items, items);
    UI::ChangeWidget (`id ("edituser"), `Enabled, size (items) > 0);
    UI::ChangeWidget (`id ("deleteuser"), `Enabled, size (items) > 0);
  }

  /**
   * Handle changes in users table
   */
  define symbol HandleAddEditUser (string key, map event) {
    if (event["ID"]:nil != key && (key != "userstable" || event["ID"]:nil != "table"))	return nil;

    if (key == "userstable") key = "edituser";
    string current_user = (string) UI::QueryWidget (`id ("table"), `CurrentItem);

    UI::OpenDialog (`opt(`decorated), `HBox (`HSpacing (0.5), `VBox (
	`VSpacing (0.5),
	// popup label
	`Label (`id (`label), _("Add new user")),
	// text entry label
	`TextEntry (`id (`username), _("&Name")),
	`Password (`id (`pw1), Label::Password(), ""),
	`Password (`id(`pw2), Label::ConfirmPassword(), ""),
	`TextEntry (`id (`home), _("&Home Directory")),
	`HBox (
	    `PushButton (`id(`ok),`opt(`key_F10), Label::OKButton()),
	    `PushButton (`id(`cancel),`opt(`key_F9), Label::CancelButton())
	),
	`VSpacing (0.5)), `HSpacing (0.5))
    );
    if (key == "edituser")
    {
	UI::ChangeWidget (`id (`username), `Value, current_user);
	UI::ChangeWidget (`id (`home), `Value, LiveISOConfig["users", current_user, "home"]:"");
	UI::ChangeWidget (`id (`pw1), `Value, "*****");
	UI::ChangeWidget (`id (`pw2), `Value, "*****");
	// popup label
	UI::ChangeWidget (`id (`label), `Value, _("Edit user"));
    }
    any ret	= nil;
    repeat
    {
	ret	= UI::UserInput ();
	if (ret == `ok)
	{
	    string username	= (string) UI::QueryWidget (`id(`username), `Value);
	    string pwd		= (string) UI::QueryWidget (`id(`pw1), `Value);
	    if (username == "")
	    {
		// popup message
		Report::Error (_("Enter the user name."));
		ret     = `notnext;
		continue;
	    }
	    if (pwd != UI::QueryWidget (`id(`pw2), `Value))
	    {
		// popup message
		Report::Error (_("The passwords do not match.
Try again."));
		ret     = `notnext;
		continue;
	    }
	    if (pwd == "")
	    {
		// popup message
		Report::Error (_("Enter the user password."));
		ret     = `notnext;
		continue;
	    }
	    if (! haskey (LiveISOConfig, "users"))
		LiveISOConfig["users"]	= $[];
	    else if (key == "edituser" && username != current_user)
		LiveISOConfig["users"]	= remove (LiveISOConfig["users"]:$[], current_user);
	    LiveISOConfig["users", username]	= $[
		"pwd"	: pwd,
		"home"	: UI::QueryWidget (`id(`home), `Value)
	    ];
	}
    } until (ret == `ok || ret == `cancel);

    UI::CloseDialog ();
    if (ret == `ok) InitUsersTable ("table");
    return nil;
  }

  /**
   * handle delete user button
   */
  define symbol HandleDeleteUser (string key, map event) {
    if (event["ID"]:nil != key)	return nil;

    string current_user = (string) UI::QueryWidget (`id ("table"), `CurrentItem);
    LiveISOConfig["users"]	= remove (LiveISOConfig["users"]:$[], current_user);
    InitUsersTable ("table");
    return nil;
  }

  /**
   * initialize (=set 'enabled' status) Create button
   */
  define void InitCreateButton (string id) {
    UI::ChangeWidget (`id (id), `Enabled, LiveISOConfig["prepared"]:false);
  }

  /**
   * handler for 'Create' action
   *
  define symbol HandleCreateButton (string key, map event) {
    if (event["ID"]:nil == key)
    {
	// popup question
	if (Popup::YesNo ("Create ISO?"))
	{
	    if (Kiwi::Create ())
	    {
		// popup message, %1 is a dir
		Popup::Message (sformat (_("ISO image successfully created in
%1
directory."), Kiwi::tmp_dir));
	    }
	}
    }
    else if (event["EventType"]:"" == "MenuEvent" || event["EventType"]:"" == "WidgetEvent")
    {
	// disable after some new changes were done
	if (LiveISOConfig["prepared"]:false && LiveISOConfig["prepared_modified"]:false)
	{
	    LiveISOConfig["prepared"]	= false;
	}
	InitCreateButton ("create");
    }
    return nil;
  }

  /**
   * handler for 'Prepare' action
   *
  define symbol HandlePrepare (string key, map event) {
    if (event["ID"]:nil == key)
    {
	if (Kiwi::WriteConfigXML (LiveISOConfig))
	{
	    if (Kiwi::Prepare ())
	    {
		Popup::Message ("Physical extend succesfully prepared.");
		LiveISOConfig["prepared"]		= true;
		LiveISOConfig["prepared_modified"]	= false; // TODO set in other handlers
	    }
	    else
	    {
		LiveISOConfig["prepared"]		= true;
	    }
	}
    }
    return nil;
  }
  */

  define symbol NotImplementedHandler (string key, map event) {
    if (event["ID"]:nil == key)
    {
	Popup::Message (_("Feature not implemented yet."));
    }
    return nil;
  }

  define boolean CreateISO (string key, map event) {
    if (event["ID"]:nil != `next)
	return true;
    // popup question
    if (Popup::YesNo ("Create ISO now?"))
    {
	if (Kiwi::WriteConfigXML (LiveISOConfig))
	{
	    if (Kiwi::PrepareAndCreate ())
	    {
		// popup message, %1 is a dir
		Popup::Message (sformat (_("ISO image successfully created in
%1
directory."), Kiwi::tmp_dir));
		return true;
	    }
	}
    }
    return false;
  }

  /********************************************************************************************
   * widget descriptions
   *******************************************************************************************/

  map tabs_descr = $[
    "config.xml" : $[
	// tab header
	"header"	: _("Image configuration"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		`VSpacing (0.2),
		`HBox (
		    `HWeight (1, `HBox (
			"isoboot",
			`VBox (`Label (""), "configure_isoboot")
		    )),
		    `HWeight (1, `VBox (`Label (""), `Left ("compressed")))
		),
		`VSpacing (0.2),
		"version",
		`HBox (
		    `HWeight (4, `VBox (
			`Left (`Label (_("Software for image"))), "rt_sw", `Right ("configure_sw")
		    )),
		    `HWeight (3, "ignored")
		    /*
		    `VBox (
			`Left (`Label (_("Packages to ignore"))),
			"rt_sw_ignore",
			`Right ("configure_sw_ignore")
		    )
		    */
		    /*
		    `VBox (
			`Left (`Label (_("Software for build"))),
			"rt_sw_boot",
			`Right ("configure_sw_boot")
		    )
		    */
		),
		/*
		`HBox (
		    "output_dir",
		    `VBox (`Label (""), "browse_output_dir")
		),
		*/
		`VSpacing (0.2)
	    ), `HSpacing(1)),
	"widget_names" : [
	    "isoboot", "configure_isoboot",
	    "compressed",
	    "version",
	    "rt_sw", "configure_sw",
	    "ignored",
//	    "rt_sw_ignore", "configure_sw_ignore",
//	    "rt_sw_boot", "configure_sw_boot",
//	    "output_dir", "browse_output_dir",
	],
    ],
    "layout" : $[
	// tab header
	"header"	: _("Directory Tree"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		`VSpacing (0.2),
		`HBox (
		    "root_dir",
		    `VBox (`Label (""), "browse_root_dir")
		),
		`HBox (
		    "config_dir",
		    `VBox (`Label (""), "browse_config_dir")
		),
		/*
		`HBox (
		    "autoyast_path",
		    `VBox (`Label (""), "browse_autoyast")
		),
		*/
		`HBox (
		    "images_path",
		    `VBox (`Label (""), "browse_images")
		),
		`HBox ("config.sh", `Bottom ("import_config.sh")),
		`VSpacing (0.2)
	    ), `HSpacing (1)),
	"widget_names" : ["root_dir", "browse_root_dir", "config_dir", "browse_config_dir",
//	    "autoyast_path", "browse_autoyast",
	    "images_path", "browse_images", "config.sh", "import_config.sh",
	],
    ],
    "description" : $[
	// tab header
	"header"	: _("Description"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		`VSpacing (0.2),
		"author",
		`VSpacing (0.2),
		"contact",
		`VSpacing (0.2),
		"specification",
		`VStretch ()
	    ), `HSpacing (1)),
	"widget_names" : [ "author", "contact", "specification" ],
    ],
    "users" : $[
	// tab header
	"header"	: _("Users"),
	"contents"	: `HBox (`HSpacing(1),
	    `VBox (
		`VSpacing (0.2),
		"group",
		`VSpacing (0.2),
		"userstable",
		`VSpacing (0.2),
		`HBox ("adduser", "edituser", "deleteuser", `HStretch ()),
		`VSpacing (0.2)
	    ), `HSpacing (1)),
	"widget_names" : [ "group", "userstable", "adduser", "edituser", "deleteuser" ]
    ],
  ];

  map<string, map> widget_descr = $[
    // global widgets
    /*
    "prepare"		: $[
	"widget"        : `push_button,
	// pusbutton label
	"label"		: _("&Prepare"),
	// help for "&Prepare" button
	"help"		: _("<p>Push <b>Prepare</b> to prepare physical extend.</p>"),
	"handle"	: HandlePrepare
    ],
    "create"		: $[
	"widget"        : `push_button,
	// pusbutton label
	"label"		: _("&Create ISO"),
	// help for "Cr&eate ISO" button
	"help"		: _("<p>Finally create the ISO image with <b>Create ISO</b>.</p>"),
	"init"		: InitCreateButton,
	"handle"	: HandleCreateButton
    ],
    */
    "global"		:$[
	"widget"	: `empty,
	"validate_type"	: `function,
	"validate_function"	: CreateISO,
	"no_help"	: true,
    ],
    // widgets for config.xml tab
    "isoboot" : $[
	"widget"	: `combobox,
	"opt"		: [ `hstretch, `notify ],
	// combo box label
	"label"		: _("C&D Boot image"),
	"help"		: _("help for isoboot"),
	"items"		: [],
	"init"		: InitISOBootCombo,
	"store"		: StoreISOBootCombo,
	"handle"	: HandleISOBootCombo,
    ],
    "configure_isoboot"	: $[
	"widget"	: `push_button,
	// pusbutton label
	"label"		: _("C&onfigure..."),
	"help"		: _("help for configure_isoboot"),
	"handle"        : NotImplementedHandler
    ],
    "compressed" : $[
	"widget"	: `checkbox,
	// textentry label
	"label"		: _("Co&mpress"),
	"help"		: _("<p>help for compressed</p>"),
	"init"		: InitCompressCheckBox,
	"store"		: StoreCompressCheckBox,
	"handle"	: HandleCompressCheckBox,
    ],
    "rt_sw"		: $[
	"widget"	: `richtext,
	"init"		: InitSWRichText,
	"help"		: "help for rt_sw",
	"label"		: "&L", // FIXME not needed, but produces error
    ],
    "configure_sw"	: $[
	"widget"        : `push_button,
	// pusbutton label
	"label"         : _("Ch&ange..."),
	"help"		: _("<p>help for configure_sw</p>"),
	"handle"	: HandleSWSelection
    ],
    "rt_sw_boot"	: $[
	"widget"	: `richtext,
	"init"		: InitSWBootRichText,
	"help"		: _("<p>help for rt_sw_boot</p>"),
	"label"		: "&L",
    ],
    "configure_sw_boot"	: $[
	"widget"        : `push_button,
	// pusbutton label
	"label"         : _("C&hange..."),
	"help"		: _("<p>help for configure_sw_boot</p>"),
    ],
    "rt_sw_ignore"	: $[
	"widget"	: `richtext,
	"init"		: InitSWIgnoreRichText,
	"help"		: _("<p>help for rt_sw_ignore</p>"),
	"label"		: "&L",
    ],
    "configure_sw_ignore"	: $[
	"widget"        : `push_button,
	// pusbutton label
	"label"         : _("C&hange..."),
	"help"		: _("<p>help for configure_sw_ignore</p>"),
    ],
    "ignored"	: $[
	"widget"	: `multi_line_edit,
	// label
	"label"		: _("&Ignored software"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
	// help text for "&Ignored software"
	"help"		: _("<p>For <b>ignored software</b>, enter each entry (like 'smtp_daemon') on new line</p>"),
    ],
    "output_dir" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("O&utput directory"),
	"help"		: _("help for output dir"),
	"init"		: InitOutputDir,
    ],
    "browse_output_dir"	: $[
	"widget"	: `push_button,
	"label"		: Label::BrowseButton (),
	"help"		: "",
    ],
    "version" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("&Version"),
	"help"		: _("<p>help for version</p>"),
	"valid_chars"	: String::CDigit () + ".",
	"init"		: InitVersion,
	"store"		: StoreVersion,
	"handle"	: HandleVersion,
    ],
    // ---------------- widgtes for directory structure
    "root_dir" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("P&ath to directory with system configuration"),
	"help"		: _("<p>this is 'root' directory</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "browse_root_dir"	: $[
	"widget"	: `push_button,
	"label"		: Label::BrowseButton (),
	"help"		: "",
    ],
    "config.sh" : $[
	"widget"	: `multi_line_edit,
	// textentry label
	"label"		: _("I&mage configuration script"),
	"help"		: _("<p>this is 'config.sh'</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "import_config.sh" : $[
	"widget"	: `push_button,
	// textentry label
	"label"		: _("&Import..."),
	"help"		: _("<p>help for import</p>"),
    ],
    "config_dir" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Pa&th to directory with scripts"),
	"help"		: _("<p>this is 'config' directory</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "browse_config_dir"	: $[
	"widget"	: `push_button,
	// push button label
	"label"		: _("Br&owse..."),
	"help"		: "",
    ],
    "autoyast_path" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Path to Auto&YaST profile"),
	"help"		: _("<p>this is 'config-yast.xml'</p>"),
    ],
    "browse_autoyast"	: $[
	"widget"	: `push_button,
	// push button label
	"label"		: _("Brow&se..."),
	"help"		: "",
    ],
    "images_path" : $[ // TODO multiline edit as well?
	"widget"	: `textentry,
	// textentry label
	"label"		: _("Pat&h to cleanup script"),
	"help"		: _("<p>this is 'images.sh'</p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "browse_images"	: $[
	"widget"	: `push_button,
	// push button label
	"label"		: _("Brows&e..."),
	"help"		: "",
    ],
    // ---------------- widgtes for description tab
    "author" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("&Author"),
	"help"		: _("<p>help for 'author' </p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "contact" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("C&ontact"),
	"help"		: _("<p>help for 'contact' </p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "specification" : $[
	"widget"	: `multi_line_edit,
	// textentry label
	"label"		: _("&Specification"),
	"help"		: _("<p>help for 'specification' </p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    // ---------------- widgtes for users tab
    "group" : $[
	"widget"	: `textentry,
	// textentry label
	"label"		: _("&Group name"),
	"help"		: _("<p>help for 'group' </p>"),
	"init"		: InitDescription,
	"store"		: StoreDescription,
	"handle"	: HandleDescription,
    ],
    "userstable" : $[
	"widget"	: `custom,
	"custom_widget" : `Table (`id("table"), `opt (`notify), `header(
			    // table header
			    _("Name"),
			    // table header
			    _("Home Directory"))),
	"help"		: _("<p>help for users table</p>"),
	"init"		: InitUsersTable,
	"handle"	: HandleAddEditUser,
    ],
    "adduser"	: $[
	"widget"	: `push_button,
	"label"		: Label::AddButton (),
	"no_help"	: true,
	"handle"	: HandleAddEditUser,
    ],
    "edituser"	: $[
	"widget"	: `push_button,
	"label"		: Label::EditButton (),
	"no_help"	: true,
	"handle"	: HandleAddEditUser,
    ],
    "deleteuser"	: $[
	"widget"	: `push_button,
	"label"		: Label::DeleteButton (),
	"no_help"	: true,
	"handle"	: HandleDeleteUser,
    ],
    // ------------------
  ];

  /**
   * Main dialog for Live media configuration
   */
  define symbol LiveDialog () {
// FIXME move common inits to some Init function

    // read the information from the base product
    CDCreator::EnableSource();
    integer src_id	= CDCreator::checkProductDependency ();
    map<string,string> read_content = CDCreator::ReadContentFile(src_id);
    y2internal ("content: %1", read_content);

    LiveISOConfig	= union (CDCreator::LiveISOConfig, $[
	"root_dir"	: images_dir + "/kwliveDVD-suse-10.3/root",
    ]);

    if (!haskey (LiveISOConfig, "isoboot") && read_content["LABEL"]:"" != "")
    {
	map label2isboot	= $[
	    "openSUSE 10.2"	: "suse-10.2",
	    "openSUSE 10.3"	: "suse-10.3",
	    "SUSE Linux 10.1"	: "suse-10.1",
	    "SUSE Linux Enterprise Server 10"	: "sles10"
	];
	LiveISOConfig["isoboot"]	= label2isboot[read_content["LABEL"]:""]:"";
    }

    string name		= LiveISOConfig["name"]:"";
    if (!haskey (LiveISOConfig, "specification"))
	LiveISOConfig["specification"]	= name;
    string configsh_file	= images_dir + "/kwliveDVD-suse-10.3/config.sh";
    if (FileUtils::Exists (configsh_file))
    {
	string configsh	= (string) SCR::Read (.target.string, configsh_file);
	if (configsh != nil)
	    LiveISOConfig["config.sh"]	= configsh;
    }

    widget_descr["tab"] = CWMTab::CreateWidget($[
        "tab_order"	: ["config.xml", "layout", "description", "users"],
	"tabs"		: tabs_descr,
	"widget_descr"	: widget_descr,
	"initial_tab"	: initial_tab,
    ]);
    Wizard::SetContentsButtons("", `VBox (), "", Label::BackButton(), Label::NextButton());

    term contents	= `VBox (
	`Left (`Label (name)),
	"tab",
	`VSpacing (0.3),
//	`HBox (`Left (`Label (name)), `Right ("prepare"), "create"),
	"global"
    );
    // dialog caption
    string caption = _("Live CD Configuration");
    symbol ret = CWM::ShowAndRun ($[
	"widget_names"		: [ "tab", "global" ],//"prepare", "create" ],
	"widget_descr"		: widget_descr,
	"contents"		: contents,
	"caption"		: caption,
	"back_button"		: Label::BackButton (),
//	"next_button"		: Label::FinishButton (),
	"next_button"		: _("&Create ISO"),
//	"abort_button"		: nil,
	"fallback_functions"	: $[
	    `back		: CreateISO,
	    `next		: CreateISO
	]
    ]);
    if (ret != `back && ret != `abort && ret != `cancel) {
	initial_tab = CWMTab::CurrentTab ();
    }
    y2milestone("Returning %1", ret);
    return ret;
  }
}
