/**
 * File:	include/cd-creator/wizards.ycp
 * Package:	Configuration of cd-creator
 * Summary:	Wizards definitions
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */

{

textdomain "cd-creator";

import "Wizard";

include "wizard/sequencer.ycp";

include "cd-creator/complex.ycp";
include "cd-creator/dialogs.ycp";




/**
 * Add a configuration of cd-creator
 * @return sequence result
 */
define symbol AddSequence() ``{


    map aliases = $[
	"source"	: ``( sourceDialog() ),
	"config1"	: ``( Configure1Dialog() ),
	"config2"	: ``( Configure2Dialog() ),
	"isolinux"	: ``( isolinuxDialog() ),
	"packagemanager": ``( packageSelector() ),
	"write"		: ``( WriteDialog() ),
	"summary"	: ``( ConfigSummary() )
    ];


    map sequence = $[
	"ws_start" : "config1",
	"source" : $[
	    `abort	: `abort,
	    `next	: "config2"
	],
	"config1" : $[
	    `abort	: `abort,
	    `next	: "source"
	],
	"config2" : $[
	    `abort	: `abort,
	    `next	: "isolinux"
	],
	"isolinux": $[
	   `abort	: `abort,
	    `next	: "packagemanager",
            `autoyast   : "summary"
	],
	"packagemanager": $[
	   `abort	: `abort,
	    `next	: "summary"
	],
	"summary" : $[
		      `abort	: `abort,
		      `next	: "write"
		      ],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	],

    ];

    return WizardSequencer( aliases, sequence );
}
/**
 * Create a CD image tree and ISO image
 * @return sequence result
 */
define symbol CreateSequence() ``{
   map aliases = $[
	"verify"	: ``( VerifyDialog() ),
	"create"	: ``( TreeDialog() )
    ];


    map sequence = $[
	"ws_start" : "verify",
	"verify" : $[
	    `abort	: `abort,
	    `next	: "create",
	    `overview	: `next
	],
	"create": $[
	    `abort	: `abort,
	    `next	: `next,
	    `overview	: `next
	]

    ];

    return WizardSequencer( aliases, sequence );
}

/**
 * Main workflow of the cd-creator configuration
 * @return sequence result
 */
define symbol MainSequence() ``{


    map aliases = $[
	"overview"	:   ``( OverviewDialog() ),
	"configure"	: [ ``( AddSequence() ), true ],
	"add"		: [ ``( AddSequence() ), true ],
	"edit"		: [ ``( AddSequence() ), true ],
	"create"	: [ ``( CreateSequence() ), true ],

    ];

    map sequence = $[
	"ws_start" : "overview",
	"overview" : $[
	    `abort	: `abort,
	    `add	: "add",
	    `edit	: "edit",
	    `create	: "create"
	],
	"configure" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"add" : $[
	    `abort	: `abort,
	    `next	: "overview",
	],
	"edit" : $[
	    `abort	: `abort,
	    `next	: "overview",
	    ],
	"create" : $[
	    `abort	: `abort,
	    `next	: "overview"
	]
    ];

   symbol ret = WizardSequencer(aliases, sequence);

    return ret;
}

/**
 * Whole configuration of cd-creator but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */

define symbol CDCreatorAutoSequence() ``{
    /* Initialization dialog caption */
    string caption = _("CD Creator Configuration");
    /* Initialization dialog contents */
    term contents = `Label(_("Initializing..."));

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("cd-creator");
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    symbol ret = MainSequence();

    UI::CloseDialog();
    return ret;
}


/**
 * Whole configuration of cd-creator
 * @return sequence result
 */
define symbol CDCreatorSequence() ``{

    map aliases = $[
	"main"	:   ``( MainSequence() ),
	"read"  : [ ``( ReadDialog() ), true ],

    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("cd-creator");

    symbol ret = WizardSequencer(aliases, sequence);

    UI::CloseDialog();
    return ret;
}


/* EOF */
}
