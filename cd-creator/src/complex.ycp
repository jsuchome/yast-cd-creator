/**
 * File:	include/cd-creator/complex.ycp
 * Package:	Configuration of cd-creator
 * Summary:	Dialogs definitions
 * Authors:	Anas Nashif <nashif@suse.de>
 *
 * $Id$
 */

{

    textdomain "cd-creator";

    import "Wizard";
    import "Popup";
    import "Wizard_hw";
    import "Report";

    import "CDCreator";

    import "Label";

    include "cd-creator/helps.ycp";
    //include "cd-creator/routines.ycp";

    /**
     * Return a modification status
     * @return true if data was modified
     */
    define boolean Modified() ``{
        return CDCreator::Modified();
    }

    /**
     * Read settings dialog
     * @return `abort if aborted and `next otherwise
     */
    define symbol ReadDialog() ``{
        Wizard::RestoreHelp(HELPS["read"]:"");
        CDCreator::AbortFunction = ``{ return CDCreator::PollAbort();};
        boolean ret = CDCreator::Read();
        return ret ? `next : `abort;
    }

    /**
     * Write settings dialog
     * @return `abort if aborted and `next otherwise
     */
    define symbol WriteDialog() ``{
        Wizard::RestoreHelp(HELPS["write"]:"");
        CDCreator::AbortFunction = ``{ return CDCreator::PollAbort();};
        boolean ret = CDCreator::Write();
        return ret ? `next : `abort;
    }


    /**
     * Overview dialog
     * @return dialog result
     */
    define symbol OverviewDialog() ``{

        /* CDCreator overview dialog caption */
        string caption = _("CD Creator Configuration Overview");

        list overview = CDCreator::Overview();

        term contents = Wizard_hw::ConfiguredContent(
                /* Table header */
                `header(_("Name"), _("Product"), _("Image")),
                overview, nil, nil, nil, nil );

        term contents2 = `VBox(
                contents,
                `PushButton(`id(`create_button), _("Create ISO Image"))
                );

        contents = Wizard_hw::SpacingAround(contents2, 1.5, 1.5, 1.0, 1.0);

        Wizard::SetContentsButtons(caption, contents, HELPS["overview"]:"",
                Label::BackButton(), Label::FinishButton());

        if (size(overview) == 0 )
        {
            UI::ChangeWidget(`id(`edit_button), `Enabled, false);
            UI::ChangeWidget(`id(`delete_button), `Enabled, false);
            UI::ChangeWidget(`id(`create_button), `Enabled, false);
        }

        any ret = nil;
        while(true) {

            ret = UI::UserInput();

            string current = (string) UI::QueryWidget(`id(`table), `CurrentItem );
            /* abort? */
            if(ret == `abort || ret == `cancel) {
                if(CDCreator::ReallyAbort()) break;
                else continue;
            }
            /* add */
            else if(ret == `add_button) {
                CDCreator::Config = $[];
                ret = `add;
                break;
            }
            /* edit */
            else if(ret == `edit_button) {
                CDCreator::Config = CDCreator::Configs[current]:$[];
                ret = `edit;
                break;
            }
            /* delete */
            else if(ret == `delete_button) {
                y2debug("Deleting: %1", current);
                CDCreator::Configs = filter(string k, map<string,any> v, CDCreator::Configs, ``(k!=current));
                overview = CDCreator::Overview();
                UI::ChangeWidget(`id(`table), `Items, overview);
                if (size(overview) == 0 )
                {
                    UI::ChangeWidget(`id(`edit_button), `Enabled, false);
                    UI::ChangeWidget(`id(`delete_button), `Enabled, false);
                    UI::ChangeWidget(`id(`create_button), `Enabled, false);
                }
                import "Progress";
                Progress::off();
                CDCreator::Write();
                Progress::on();
         continue;
            }
            /* create */
            else if(ret == `create_button) {
                string current = (string) UI::QueryWidget(`id(`table), `CurrentItem );
                CDCreator::Config = CDCreator::Configs[current]:$[];
                // CDCreator::EnableSource();
                ret = `create;
                break;
            }
            else if (ret == `next || ret == `back) {
                break;
            }
            else {
                y2error("unexpected retcode: %1", ret);
                continue;
            }
        }

        return (symbol)ret;
    }



    /**
     * TreeDialog
     * Dialog for creating the skeleton and copying common data.
     * @return symbol
     */
    define symbol TreeDialog () ``{

        import "Progress";


        string help = _("<P>
                Please wait while the directory structure for the new ISO image is created.
                </P>
                
");
        help = help + _("<p>Press <b>Next</b> to start creating the ISO file.</p>");



        list<string> progress_stages =
            [
            _("Create skeleton with common files"),
        _("Copy additional and customized files"),
        _("Copy selected packages")
            ];

        list<string> progress_descriptions =
            [

            _("Creating skeleton with essential files"),
        _("Copying additional and customized files to directory tree..."),
        _("Copying selected packages")
            ];


        Progress::New(
                _("Creating ISO image directory..."),
                "",     // progress_title
                size( progress_stages ) + size(CDCreator::toCopy) + 1,
                progress_stages,
                progress_descriptions,
                help );


        if(CDCreator::Abort()) return `abort;
        Progress::NextStage();

        if (!CDCreator::CreateSkeleton())
        {
            Report::Error(_("Error while creating skeleton."));
            return `overview;
        }


        if(CDCreator::Abort()) return `abort;
        Progress::NextStage();
        if (CDCreator::Config["pkgtype"]:"" == "autoyast")
        {
            CDCreator::CopyMiscFiles();
        }
        else 
        {
           if (CDCreator::Config["bootconfig"]:"" != "")
            {
                y2debug("bootconfig available");
                SCR::Write(.target.string,
                        sformat("%1/boot/loader/isolinux.cfg", CDCreator::skel_root),
                        CDCreator::Config["bootconfig"]:"" );
            }

        }

        Progress::NextStage();
        if (!CDCreator::CopyPackages())
        {
            return `overview;
        }

        integer i = 0;


        Progress::Title(_("ISO image directory ready"));
        Progress::Finish();

        Wizard::EnableNextButton();
        Wizard::RestoreNextButton();

        any ret  = nil;
        repeat
        {
            ret = UI::UserInput();
            if (ret == `next)
            {
                if ( CDCreator::Config["result"]:"" == "iso")
                {
                    string isodir = CDCreator::Config["iso-directory"]:"/tmp" + "/" +  CDCreator::Config["name"]:"tmp";
                    string isofile = CDCreator::Config["iso-directory"]:"" + "/" +  CDCreator::Config["isofile"]:"";
                    string pub = CDCreator::Config["publisher"]:"anon";
                    string prep = CDCreator::Config["preparer"]:"anon";

                    string du = lookup((map)SCR::Execute(.target.bash_output, sformat("du -s -b %1 | awk -F' ' ' { printf $1 }'", isodir )), "stdout", "");
                    y2debug("Expected size: %1", du);
                    Popup::ShowFeedback(_("Creating CD Image..."), _("This may take a while"));
                    SCR::Execute (.target.bash, "/usr/bin/y2mkiso $ISODIR $ISOFILE", $["ISOFILE": isofile, "ISODIR": isodir, "CD_PUBLISHER": pub, "CD_PREPARER": prep ]);
                    Popup::ClearFeedback();
                }
                ret = `next;
            }

        } until (ret == `next || ret == `back || ret == `abort);
        return (symbol)ret;
    }

    /**
     * ISO Summary
     * @return symbol
     */
    define symbol isoSummary() ``{
        import "HTML";

        // caption
        string caption = _("ISO Summary");
        string html =  HTML::Heading(_("Package Source"));
        html = html + HTML::Para(CDCreator::Config["source"]:"");

        html = html +  HTML::Heading(_("Packages"));
        html = html + HTML::Para(sformat("%1", size(Pkg::GetPackages(`selected, true))));

        html = html +  HTML::Heading(_("Missing Packages"));
        if (size(CDCreator::missing_packages) > 0 ) {
            html = html + HTML::List(CDCreator::missing_packages);
        } else {
            html = html + HTML::Para(_("None"));
        }

        term contents =
            `RichText(html);

        string help_text = _("<p>Verify if the data in the summary box is correct then
press Finish to return to main dialog.</p>
");

        Wizard::SetContentsButtons ( caption,
                contents,
                help_text,
                Label::BackButton (),
                Label::NextButton ());
        any ret = nil;
        repeat
        {
            ret = UI::UserInput();
        }  until (ret == `next || ret == `back || ret == `abort);
        return (symbol)ret;
    }

    /**
     * VerifyDialog()
     * Verify Dialog
     *
     */
    define symbol VerifyDialog()
        ``{

            import "Progress";


            // caption
            string caption = _("Custom CDs");

            string help = _("<p>Please wait while the data and packages are verified...
                    </p>
                    ");
            help = help +  _("<p>If there is something missing, the process will be aborted.
Fix the problem and try again.</p>
                    ");

            list<string> progress_stages =
                [
                _("Set up Package Source"),
            _("Create Package List"),
            _("Verify Package Availability"),
            _("Check Destination")
                ];

            list<string> progress_descriptions =
                [
                _("Configuring package source..."),
            _("Creating package list..."),
            _("Verifying package availability..."),
            _("Checking destination...")
                ];

            Progress::New(
                    _("Verification of Data for ISO Image"),
                    "",     // progress_title
                    size( progress_stages ),
                    progress_stages,
                    progress_descriptions,
                    help );


            boolean success = true;


            Progress::NextStage();
            Pkg::TargetFinish ();

            string tmp = (string)SCR::Read( .target.tmpdir );
            SCR::Execute(.target.mkdir, tmp + "/tmproot");
            Pkg::TargetInit( tmp + "/tmproot" , true);


            success =  CDCreator::EnableSource();

            Progress::NextStage();
            success = CDCreator::setPackages();


            Progress::NextStage();
            string error_msg = CDCreator::checkPackageAvail();
            if (error_msg != "")
            {
                Popup::Error(error_msg);
                return `back;
            }

            Progress::NextStage();
            if (!CDCreator::verifyDestination())
                return `overview;

            Progress::Finish();

            Wizard::EnableNextButton();
            Wizard::RestoreNextButton();
            Wizard::EnableBackButton();
            any ret = nil;
            repeat
            {
                ret = UI::UserInput();
            }  until (ret == `next || ret == `back || ret == `abort);

            return (symbol)ret;
        }






    /* EOF */
}
